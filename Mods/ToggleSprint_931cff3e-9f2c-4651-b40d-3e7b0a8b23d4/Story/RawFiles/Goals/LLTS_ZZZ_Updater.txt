Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLTS_Updater_RegisterVersion("2.5.1.0");
LLTS_System_RegisterDatabases();
KBSECTION
QRY
LLTS_Updater_QRY_UpdateDetected()
AND
NOT DB_Mods_Registered("ToggleSprint", "LaughingLeader", "2.5.1.0")
THEN
LLTS_Updater_RegisterVersion("2.5.1.0");

//Before the version/author placement swap
PROC
LLTS_Updater_RemoveOldVersions()
AND
DB_Mods_Registered("ToggleSprint", _Version, "LaughingLeader")
THEN
NOT DB_Mods_Registered("ToggleSprint", _Version, "LaughingLeader");

PROC
LLTS_Updater_RemoveOldVersions()
AND
DB_Mods_Registered("ToggleSprint", "LaughingLeader", _Version)
THEN
NOT DB_Mods_Registered("ToggleSprint", "LaughingLeader", _Version);

PROC
LLTS_Updater_RegisterVersion((STRING)_Version)
THEN
LLTS_Updater_RemoveOldVersions();
LLTS_Updater_Fix_RemoveSprintFromPlayers();
DB_Mods_Registered("ToggleSprint", "LaughingLeader", _Version);

//For previous saves with double sprint statuses
PROC
LLTS_Updater_Fix_RemoveSprintFromPlayers()
AND
DB_ToggleSprint_Settings_SpeedSet(_Index)
AND
NOT DB_LLTS_Updater_Temp_LastSpeed(_)
THEN
SysClear("DB_ToggleSprint_Settings_SpeedSet", 1);
DB_LLTS_Updater_Temp_LastSpeed(_Index);

PROC
LLTS_Updater_Fix_RemoveSprintFromPlayers()
AND
DB_IsPlayer(_Player)
AND
LLTS_QRY_HasSprintStatus(_Player)
THEN
LLTS_Clear_SprintActive(_Player);
LLTS_Debug_ClearSpeedStatus(_Player);
DB_LLTS_Updater_Temp_WasSprinting(_Player);

IF
GameStarted(_,_)
AND
LLTS_Updater_QRY_UpdateDetected()
THEN
LLTS_System_RefreshDatabases();

IF
GameStarted(_,_)
AND
DB_LLTS_Updater_Temp_LastSpeed(_Index)
THEN
NOT DB_LLTS_Updater_Temp_LastSpeed(_Index);
LLTS_UpdateSpeed(_Index);

IF
GameStarted(_,_)
AND
DB_LLTS_Updater_Temp_WasSprinting(_Player)
AND
CharacterIsInCombat(_Player, 0)
THEN
LLTS_ApplySpeed(_Player);
NOT DB_LLTS_Updater_Temp_WasSprinting(_Player);

IF
GameStarted(_,_)
AND
DB_LLTS_Updater_Temp_WasSprinting(_Player)
THEN
NOT DB_LLTS_Updater_Temp_WasSprinting(_Player);

PROC
LLTS_System_RefreshDatabases()
THEN
DebugBreak("[LLTS:Updater] Databases refreshed.");

PROC
LLTS_System_RegisterDatabases()
THEN
DebugBreak("[LLTS:Updater] Databases registered.");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_ToggleSprint"
