INIT
	CHARACTER:__Me
	INT:%SprintEnabled = 0
	INT:%AutoSprintEnabled = 1
	EXTERN FIXEDSTRING:%SprintCombatMessage = "Sprint cannot be used in combat."
	EXTERN ITEMTEMPLATE:%AutoSprintItemTemplate = "Quest_AutoSprint_be19d1b1-66aa-4660-b809-10b4b72446c3"
	EXTERN SKILL:%SprintSkill = "Shout_Player_ToggleSprint"
	FIXEDSTRING:%SprintInitTimerName = "SprintingInitTimer"

EVENTS
EVENT FirstLoadSetup
ON
	OnLoaded(_,_,_,_)
ACTIONS
IF "c1&c2&c3&!c4"
	CharacterHasSkill(__Me, %SprintSkill)
	IsGreaterThen(%SprintEnabled, 0)
	IsGreaterThen(%AutoSprintEnabled, 0)
	IsInCombat(__Me)
THEN
	CharacterApplyStatus(__Me, PLAYER_TOGGLESPRINT, -1)
ENDIF
	
EVENT InitSprinting
VARS
	ITEM:_Item
	FIXEDSTRING:_Stats //"QUEST_AutoSprint"
ON
	OnInit()
	OnLoaded(_,_,_,_)
	OnActivate()
	OnCharacterCreationStopped(__Me)
ACTIONS
IF "!c1"
	CharacterInCreation(__Me)
THEN
	IF "!c1"
		CharacterHasSkill(__Me, %SprintSkill)
	THEN
		CharacterAddSkill(__Me, %SprintSkill, 0)
	ENDIF
ENDIF

EVENT ToggleSprint
VARS
	ITEM:_Item
	FIXEDSTRING:_Stats //"QUEST_AutoSprint"
ON
	OnSkillCast(__Me, %SprintSkill)
ACTIONS
IF "!c1"
	IsInCombat(__Me)
THEN
	IF "c1"
		CharacterHasStatus(__Me, PLAYER_TOGGLESPRINT)
	THEN
		CharacterRemoveStatus(__Me, PLAYER_TOGGLESPRINT)
		Set(%SprintEnabled, 0)
	ELSE
		CharacterApplyStatus(__Me, PLAYER_TOGGLESPRINT, -1)
		Set(%SprintEnabled, 1)
			
		IF "!c1"
			ItemGetFromInventory(_Item, __Me, _Stats, "AUTOSPRINT", 0)
		THEN
			SpawnItem(%AutoSprintItemTemplate, __Me, _Item)
			ItemToInventory(_Item, __Me)
		ENDIF
	ENDIF
ELSE
	StatusText(__Me, %SprintCombatMessage)
ENDIF
	
EVENT AutoSprintOn
ON
	OnLeftCombat(__Me, _)
ACTIONS
IF "c1&c2"
	IsGreaterThen(%SprintEnabled, 0)
	IsGreaterThen(%AutoSprintEnabled, 0)
THEN
	CharacterApplyStatus(__Me, PLAYER_TOGGLESPRINT, -1)
ELSE
	Set(%SprintEnabled, 0)
ENDIF

EVENT AutoSprintOff
ON
	OnSkillDeactivated(__Me, %SprintSkill)
	OnEnteredCombat(__Me, _)
ACTIONS
IF "c1"
	CharacterHasStatus(__Me, PLAYER_TOGGLESPRINT)
THEN
	CharacterRemoveStatus(__Me, PLAYER_TOGGLESPRINT)
ENDIF